//import org.gradle.api.tasks.application.CreateStartScripts

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'

archivesBaseName = 'jumbodb-starter'
applicationName = 'jumbodb'

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/java'
        }
    }
    test {
        resources {
            srcDir 'src/test/resources'
            srcDir 'src/test/java'
        }
    }
}

mainClassName = "org.jumbodb.starter.JumboDBStarter"
applicationDefaultJvmArgs = ["-Xmx2G"]

dependencies {
//    compile 'javax.servlet:servlet-api:2.5'
    compile 'commons-lang:commons-lang:2.6'
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-logging-log4j:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    compile "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
    compile "org.eclipse.jdt.core.compiler:ecj:3.6"
}

applicationDistribution.from('../README.md'){
    into "/"
}

applicationDistribution.from('../database/LICENSE'){
    into "/"
}

def retrieveWarFileName() {
    def p = project(':database')
    'jumbodb-' + p.version + '.war'
}

def retrieveImportFileFileName() {
    def p = project(':importer:hadoop-json')
    p.applicationName + '-' + p.version
}

def retrieveJarFileName() {
    archivesBaseName + '-' + version + '.jar'
}

def retrieveTargetWarPath() {
    '/war/' + retrieveWarFileName()
}

def retrieveTargetJarPath() {
    '/lib/' + retrieveJarFileName()
}

def locateWarPath() {
    def p = project(':database')
    new File(p.libsDir.path + '/' + retrieveWarFileName())
}

def locateImporterPath() {
    def p = project(':importer:hadoop-json')
    new File(p.buildDir.path + '/distributions/' + retrieveImportFileFileName() + '.zip')
}

def locateHadoopJarPath() {
    def p = project(':importer:hadoop-json')
    p.libsDir
}


task copyWar(dependsOn: ':database:war') {
    doLast {
        def path = locateWarPath()
        println("Copying WAR File " + path)
        applicationDistribution.from(path) {
            into "war"
        }
    }
}
jar.dependsOn copyWar

task copyImporter(dependsOn: ':importer:hadoop-json:distZip') {
    doLast {
        def path = locateImporterPath()
        println("Copying Importer " + path)
        def zipPath = zipTree(path)
        def extractionPath = buildDir.path + '/tmp.extracted/';
        copy {
            from zipPath
            into extractionPath
        }

        applicationDistribution.from(extractionPath + retrieveImportFileFileName() + "/") {
            into 'tools/import-cli/'
        }

        applicationDistribution.from(locateHadoopJarPath()) {
            into 'tools/import-hadoop/'
        }
    }
}
jar.dependsOn copyImporter

jar {
    manifest {
        attributes("warPath": retrieveTargetWarPath())
        attributes("jarPath": retrieveTargetJarPath())
    }
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}


idea {
    module {
        downloadSources = true
    }
}