defaultTasks 'build'

apply plugin: 'java'
apply plugin: 'maven'
archivesBaseName = 'jumbodb-hadoop-connector'

repositories {
    mavenCentral()
    mavenRepo(url: "http://repository-comsysto.forge.cloudbees.com/release")
    mavenRepo(url: "http://repository-comsysto.forge.cloudbees.com/snapshot")
}

configurations {
    deployerJars
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/java'
        }
    }
    test {
        resources {
            srcDir 'src/test/resources'
            srcDir 'src/test/java'
        }
    }
}

dependencies {
    deployerJars ("org.apache.maven.wagon:wagon-webdav:1.0-beta-2")
    compile project(':connectors:jumbodb-java-connector')

    compile "org.apache.hadoop:hadoop-core:1.0.3"
    compile 'commons-io:commons-io:2.3'
    testCompile "junit:junit:4.10"
    testCompile "org.mockito:mockito-core:1.9.0"
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}
artifacts {
    archives(packageJavadoc) {
        type = 'javadoc'
        extension = 'jar'
    }
    archives(packageSources)
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            snapshotRepository(id: 'cloudbees', url: uri("dav:https://repository-${project.cloudbeesAccountName}.forge.cloudbees.com/snapshot/")) {
                authentication(userName: project.cloudbeesUsername, password: project.cloudbeesPassword)
            }
            repository(id: 'cloudbees', url: uri("dav:https://repository-${project.cloudbeesAccountName}.forge.cloudbees.com/release/")) {
                authentication(userName: project.cloudbeesUsername, password: project.cloudbeesPassword)
            }

            pom.project {
                name 'jumboDB hadoop connector'
                packaging 'jar'
                description 'jumboDB hadoop connector'
                url 'http://www.jumbodb.org'
                inceptionYear '2013'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'chufe'
                        name 'Carsten Hufe'
                        email 'carsten.hufe@comsysto.com    '
                        roles {
                            role 'Developer'
                        }
                    }
                }
            }
        }
    }
}

// generate gradle bootstrapping
// run gradle createWrapper
task createWrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}
