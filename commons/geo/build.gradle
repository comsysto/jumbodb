defaultTasks 'build'

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'project-reports'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: "groovy"

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'org.jumbodb.common.geo'
archivesBaseName = 'jumbodb-geo'

repositories {
    mavenCentral()
}


configurations {
    // this scope extracts the files and copies it into he same jar file, because ou can just use one jar file in emr
    deployerJars
}

dependencies {
    deployerJars ("org.apache.maven.wagon:wagon-webdav:1.0-beta-2")
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "org.codehaus.groovy:groovy-all:2.0.8"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}
artifacts {
    archives(packageJavadoc) {
        type = 'javadoc'
        extension = 'jar'
    }
    archives(packageSources)
}


uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            snapshotRepository(id: 'cloudbees', url: uri("dav:https://repository-${project.cloudbeesAccountName}.forge.cloudbees.com/snapshot/")) {
                authentication(userName: project.cloudbeesUsername, password: project.cloudbeesPassword)
            }
            repository(id: 'cloudbees', url: uri("dav:https://repository-${project.cloudbeesAccountName}.forge.cloudbees.com/release/")) {
                authentication(userName: project.cloudbeesUsername, password: project.cloudbeesPassword)
            }

            pom.project {
                name 'jumboDB commons geo'
                packaging 'jar'
                description 'jumboDB commons geo'
                url 'http://www.jumbodb.org'
                inceptionYear '2013'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'chufe'
                        name 'Carsten Hufe'
                        email 'carsten.hufe@comsysto.com    '
                        roles {
                            role 'Developer'
                        }
                    }
                }
            }
        }
    }
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}

idea {
    module {
        downloadSources = true
    }
}