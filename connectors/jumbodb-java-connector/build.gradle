apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "groovy"

archivesBaseName = 'jumbodb-java-connector'

sourceSets {
    main {
        resources {
            srcDir 'src/main/resources'
            srcDir 'src/main/java'
        }
    }
    test {
        resources {
            srcDir 'src/test/resources'
            srcDir 'src/test/java'
            srcDir 'src/test/groovy'
        }
    }
}


configurations {
    deployerJars
}

dependencies {
    deployerJars ("org.apache.maven.wagon:wagon-webdav:1.0-beta-2")
    compile project(':commons:query')
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.7'
    compile 'commons-lang:commons-lang:2.5'
    compile 'commons-io:commons-io:2.3'
    compile 'commons-collections:commons-collections:3.2.1'
    compile 'com.google.guava:guava:13.0'
    compile "org.xerial.snappy:snappy-java:$snappyVersion"
    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "org.codehaus.groovy:groovy-all:2.0.8"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}


task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}
artifacts {
    archives(packageJavadoc) {
        type = 'javadoc'
        extension = 'jar'
    }
    archives(packageSources)
}


uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            snapshotRepository(id: 'cloudbees', url: uri("dav:https://repository-${project.cloudbeesAccountName}.forge.cloudbees.com/snapshot/")) {
                authentication(userName: project.cloudbeesUsername, password: project.cloudbeesPassword)
            }
            repository(id: 'cloudbees', url: uri("dav:https://repository-${project.cloudbeesAccountName}.forge.cloudbees.com/release/")) {
                authentication(userName: project.cloudbeesUsername, password: project.cloudbeesPassword)
            }

            pom.project {
                name 'jumboDB java connector'
                packaging 'jar'
                description 'jumboDB java connector'
                url 'http://www.jumbodb.org'
                inceptionYear '2013'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'chufe'
                        name 'Carsten Hufe'
                        email 'carsten.hufe@comsysto.com    '
                        roles {
                            role 'Developer'
                        }
                    }
                }
            }
        }
    }
}

// https://github.com/ari/gradle-release-plugin/blob/master/build.gradle


eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = false
    }
}

idea {
    module {
        downloadSources = true
    }
}
